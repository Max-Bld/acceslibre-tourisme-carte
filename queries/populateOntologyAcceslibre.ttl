prefix fx: <http://sparql.xyz/facade-x/ns/>
prefix xyz: <http://sparql.xyz/facade-x/data/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <http://schema.org/>
prefix datatourisme: <https://www.datatourisme.fr/ontology/core#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix kb: <https://www.datatourisme.fr/resource/core#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix acceslibre: <https://acceslibre.beta.gouv.fr/ontology#>

CONSTRUCT {
    ?id a ?class ;
        rdfs:label ?nom ;
        datatourisme:isLocatedAt ?place ;
        acceslibre:hasAccessibility ?accessibility.

    ?place a datatourisme:Place ;
        schema:address ?address ;
        schema:geo ?geo .

    ?geo a schema:GeoCoordinates ;
        schema:latitude ?lat ;
        schema:longitude ?lon ;
        datatourisme:latlon ?latlon.

    ?address a schema:PostalAddress, datatourisme:PostalAddress ;
        schema:addressLocality ?commune ;
        schema:postalCode ?codePostal ;
        schema:streetAddress ?adresse.

    ?contact a foaf:Agent, datatourisme:Agent ;
        foaf:homepage ?url ;
        schema:telephone ?telephone.

    ?description a datatourisme:Description ;
        dc:description ?histoire ;
        datatourisme:shortDescription ?atout;
        owl:topDataProperty ?interet.

    ?accessibility a acceslibre:Accessibility ;
        acceslibre:parkingInTheFacility ?stationnement_presence ;
        acceslibre:parkingNearTheFacility ?stationnement_ext_presence ;
        acceslibre:adaptedParkingCloseToTheFacility ?stationnement_ext_pmr ;
        acceslibre:accessAdditionalInformation ?transport_information ;

        acceslibre:easilyIdentifiableEntrance ?entree_reperage ;
        acceslibre:hasADoor ?entree_porte_presence ;
        acceslibre:glazedEntrance ?entree_vitree ;

        acceslibre:outsidePath ?cheminement_ext_presence ;

        acceslibre:visibleReceptionArea ?accueil_visibilite;
        acceslibre:pathwayBetweenEntranceAndReception ?accueil_cheminement_plain_pied


}

WHERE {
    SERVICE <x-sparql-anything:> {
        fx:properties
            fx:location "./data/acceslibre/results-acceslibre-0.json" ;
            fx:media-type "application/json".

    [] fx:anySlot [
        xyz:uuid ?uuid ;
        xyz:nom ?nom ;
        xyz:activite [
            xyz:nom ?nomActivite
            ] ;
        xyz:adresse ?adresse ;
        xyz:commune ?commune ;
        xyz:code_postal ?codePostal ;
        xyz:geom [
            xyz:coordinates [ rdf:_1 ?lon; rdf:_2 ?lat]
            ] ;
        xyz:site_internet ?url ;
        xyz:telephone ?telephone ;
        xyz:accessibilite [
            xyz:transport [
                xyz:stationnement_presence ?stationnement_presence ;
                xyz:stationnement_ext_presence ?stationnement_ext_presence ;
                xyz:stationnement_ext_pmr ?stationnement_ext_pmr ;
                xyz:transport_information ?transport_information
                ] ;
            xyz:entree [
                xyz:entree_porte_presence ?entree_porte_presence ;
                xyz:entree_reperage ?entree_reperage ;
                xyz:entree_vitree ?entree_vitree
                ] ;
            xyz:cheminement_ext [
                xyz:cheminement_ext_presence ?cheminement_ext_presence
                ] ;
            xyz:accueil [
                xyz:accueil_visibilite ?accueil_visibilite ;
                xyz:accueil_cheminement_plain_pied ?accueil_cheminement_plain_pied
                ]
        ]
        ]
    }

BIND(IRI(CONCAT("https://www.datatourisme.fr/ontology/core#", ?uuid)) AS ?id)
BIND(IRI(CONCAT("https://www.datatourisme.fr/ontology/core#", STRUUID())) AS ?place)
BIND(IRI(CONCAT("https://www.datatourisme.fr/ontology/core#", STRUUID())) AS ?accessibility)
BIND(IRI(CONCAT("https://www.datatourisme.fr/ontology/core#", STRUUID())) AS ?address)
BIND(IRI(CONCAT("https://www.datatourisme.fr/ontology/core#", STRUUID())) AS ?geo)
BIND(IRI(CONCAT("https://www.datatourisme.fr/ontology/core#", STRUUID())) AS ?contact)
BIND(IRI(CONCAT("https://www.datatourisme.fr/ontology/core#", STRUUID())) AS ?description)

BIND (
  COALESCE(
    IF(?nomActivite = "Restauration Rapide", IRI("https://www.datatourisme.fr/ontology/core#FastFoodRestaurant"), 1/0),
    IF(?nomActivite = "Droguerie", IRI("https://www.datatourisme.fr/ontology/core#Store"), 1/0),
    IF(?nomActivite = "Office du tourisme", IRI("https://www.datatourisme.fr/ontology/core#LocalTouristOffice"), 1/0),
    IF(?nomActivite = "Cimetière", IRI("https://www.datatourisme.fr/ontology/core#RemembranceSite"), 1/0),
    IF((?nomActivite = "Ost\u00e9opathie" || ?nomActivite = "Herboristerie naturopathie"), IRI("https://www.datatourisme.fr/ontology/core#HealthcareProfessional"), 1/0),
    IF(?nomActivite = "Caf\u00e9, bar, brasserie", IRI("https://www.datatourisme.fr/ontology/core#BarOrPub"), 1/0),
    IF(?nomActivite = "Bien-être", IRI("https://www.datatourisme.fr/ontology/core#ServiceProvider"), 1/0),
    IF(?nomActivite = "Hôtel restaurant", IRI("https://www.datatourisme.fr/ontology/core#HotelRestaurant"), 1/0),
    IF(?nomActivite = "Hôtel", IRI("https://www.datatourisme.fr/ontology/core#Hotel"), 1/0),
    IF(?nomActivite = "Bijouterie joaillerie", IRI("https://www.datatourisme.fr/ontology/core#CraftsmanShop"), 1/0),
    IRI("https://www.datatourisme.fr/ontology/core#PointOfInterest")
  ) AS ?class
)

BIND(STRDT(CONCAT(STR(?lat), "#", STR(?lon)), <http://www.bigdata.com/rdf/geospatial/literals/v1#lat-lon>) AS ?latlon)
}